WHITESPACE = _{ " " | "\t"}
COMMENT = _{("//"|"#") ~ (!(NEWLINE|EOI) ~ ANY)* ~ (NEWLINE|EOI) }

special_symbol = {"^"|"."|","|"["|"]"|"{"|"}"|"("|")"|"\\"}
alphabet = _{(!special_symbol)~'\x21'..'\x7f' | "\\" ~ ("-" | special_symbol)}

direction_list = @{ ("<" | ">" | "-")+}
no_br_list_d = {direction_list}
square_br_list_d = {"[" ~ direction_list ~ "]"}
curly_br_list_d = {"{" ~ direction_list ~ "}"}
direction = _{ no_br_list_d | square_br_list_d | curly_br_list_d}

symbol_list = @{(alphabet ~ ".." ~ alphabet | alphabet)+}
compl = {"^"}
no_br_list = ${compl? ~ symbol_list}
square_br_list = ${"[" ~ compl? ~ symbol_list ~ "]"}
curly_br_list = {"{" ~ symbol_list ~ "}"}
symbol = _{ no_br_list | square_br_list | curly_br_list | compl}
string = ${(alphabet)+}
state = @{string? ~ (square_br_list | curly_br_list)? ~ string?}

rule = {"(" ~ state ~ "," ~ symbol ~ "," ~ state ~ "," ~ symbol ~ "," ~ direction ~ ")"}

rules = {SOI ~ NEWLINE* ~ (rule ~ NEWLINE*)* ~ EOI}